      *------------------------------------------------------------------*
      * TIME002  - Time-out example using error indicator                *
      *------------------------------------------------------------------*
     FTIME002DCF  E                    WORKSTN      KINFDS DSPINF
     F                                              KNUM        1
     IDSPINF      DS
     I                                     *STATUS  ##STS
     I                                      369 369 ##AID
      *
     I              X'33'                 C         ##F3
     I              X'3C'                 C         ##F12
     I              'Y'                   C         YES
     I              'N'                   C         NO
      *
      *   Stay in program until Exit is requested
      *
     C           EXIT      DOUEQYES
      *
     C                     WRITEFMT1
     C                     READ TIME002D               8990
      *
     C                     SELEC
     C           ##AID     WHEQ ##F3                       Exit
     C                     MOVE YES       EXIT    1
     C           ##AID     WHEQ ##F12                      Cancel
     C                     MOVE YES       EXIT
     C           ##STS     WHEQ 1331                       Time-out
     C                     CALL 'QCMDEXC'
     C                     PARM 'DSCJOB'  CMD     6
     C                     PARM 6         CMDLEN 155
     C                     ENDSL
      *
     C                     ENDDO
      *
      *
     C                     MOVE *ON       *INLR            SETON LR
     C                     RETRN                            AND RETURN
